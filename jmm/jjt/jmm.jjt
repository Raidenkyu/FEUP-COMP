// clang-format off
options {
    LOOKAHEAD = 1;
    FORCE_LA_CHECK = true;
    TRACK_TOKENS = true;
}

PARSER_BEGIN(jmm)
package jjt;

import java.io.*;

public class jmm {
    static boolean has_error = false, parsed = false;
    static int[][] tokenCache;

    static void pushTokenCache() {
        jmm.tokenCache = new int[2][2];
        jmm.tokenCache[0][0] = jmm.token.beginLine;
        jmm.tokenCache[0][1] = jmm.token.beginColumn;
        jmm.tokenCache[1][0] = jmm.token.endLine;
        jmm.tokenCache[1][1] = jmm.token.endColumn + 1;
    }

    /**
     * Called directly through the command line, used on checkpoint 1.
     */
    public static void main(String[] args) {
        if (args.length == 0) {
            System.err.println("Usage: java jjt.jmm java-file [number of recoveries]");
            System.exit(1);
        }

        File file = new File(args[0]);
        if (args.length > 1) ParseException.numberRecoveries = Integer.parseInt(args[1]);

        try {
            SimpleNode root = parseClass(file);
            if (root != null) root.dump("");
        } catch (FileNotFoundException e) {
            System.err.println(e.getMessage());
            System.exit(1);
        } catch (ParseException e) {
            System.err.println(e.getMessage());
            System.exit(1);
        }
    }

    public static SimpleNode parseClass(File file) throws FileNotFoundException, ParseException {
        FileReader fr = new FileReader(file);
        ParseException.file = file.toString();
        jmm myProg = new jmm(fr);

        SimpleNode root = jmm.Program();
        return root;
    }

    public static SimpleNode parseAssignment(File file) throws FileNotFoundException, ParseException {
        FileReader fr = new FileReader(file);
        ParseException.file = file.toString();
        jmm myProg = new jmm(fr);

        SimpleNode root = jmm.ProgramAssignment();
        return root;
    }

    public static SimpleNode parseExpression(File file) throws FileNotFoundException, ParseException {
        FileReader fr = new FileReader(file);
        ParseException.file = file.toString();
        jmm myProg = new jmm(fr);

        SimpleNode root = jmm.ProgramExpression();
        return root;
    }

    public static SimpleNode parseTerm(File file) throws FileNotFoundException, ParseException {
        FileReader fr = new FileReader(file);
        ParseException.file = file.toString();
        jmm myProg = new jmm(fr);

        SimpleNode root = jmm.ProgramTerm();
        return root;
    }
}
PARSER_END(jmm)

SKIP: {
    " " | "\r" | "\t" | "\n"
    |
    < "//" (~["\n","\r"])* ("\n" | "\r") > // skip single-line comments
    |
    < "/*" (~["*"])* "*" (~["*","/"] (~["*"])* "*" | "*")* "/" > // skip multiline comments
}

TOKEN: {
    // Keywords
    <PUBLIC: "public">
    |
    <STATIC: "static">
    |
    <CLASS: "class">
    |
    <EXTENDS: "extends">
    |
    <RETURN: "return">
    |
    <IF: "if">
    |
    <ELSE: "else">
    |
    <WHILE: "while">
    |
    <NEW: "new">
    |
    <THIS: "this">
    |
    <TRUE: "true">
    |
    <FALSE: "false">
    |
    // Builtin types
    <VOID: "void">
    |
    <INT: "int">
    |
    <BOOLEAN: "boolean">
    |
    // Operators
    <COMMA: ",">
    |
    <SEMICOLON: ";">
    |
    <EQUAL: "=">
    |
    <AND: "&&">
    |
    <LT: "<">
    |
    <SUM: "+">
    |
    <SUB: "-">
    |
    <MUL: "*">
    |
    <DIV: "/">
    |
    <NOT: "!">
    |
    <POINT: ".">
    |
    // Special strings
    <MAIN: "main">
    |
    <STRING: "String">
    |
    <LENGTH: "length">
    |
    // Brackets
    <LEFTKEY: "{">
    |
    <RIGHTKEY: "}">
    |
    <LSB: "[">  // Left Square Bracket
    |
    <RSB: "]">  // Right Square Bracket
    |
    <LCB: "(">  // Left Curved Bracket
    |
    <RCB: ")">  // Right Curved Bracket
    |
    // Integer Literal (decimal | hexadecimal | binary)
    <INTEGERLITERAL: (((["+","-"])?(["0"-"9"])+)|("0x"(["0"-"9","a"-"f","A"-"F"])+)|("0b"(["0","1"])+))>
    |
    // Identifier
    <IDENTIFIER: ["A"-"Z","a"-"z","$","_"](["0"-"9","A"-"Z","a"-"z","_","$"])*>
    |
    <ARR: "@">
}

SimpleNode Program() : {}
{
    ClassDeclaration()
    {return jjtThis;}
}

SimpleNode ProgramExpression() : {}
{
    Expression()
    {return jjtThis;}
}

SimpleNode ProgramTerm() : {}
{
    Term4()
    {return jjtThis;}
}

SimpleNode ProgramAssignment() : {}
{
    IdentifierStatement()
    {return jjtThis;}
}

// According to the Java Identifier specification, 'length' is a valid
// identifier. So we should accept method names called 'length' along with
// the construct 'variable.length'. This generates a difficult lookahead issue
// at ObjectAccessTail() and then LengthTail(), as the two different constructs
// Length(1) and Call(3) interact with the previous <LENGTH> token differently.
// The first does not add <LENGTH> to the AST, while the second does, and it
// needs its begin and end line/column. So we add <LENGTH> to the jmm cache,
// and 'pop' it only if necessary.
void Length() #void : {}
{
    <LENGTH>
    {jmm.pushTokenCache();}
}

void MethodNameLength() #MethodName : {}
{
    {jjtThis.jjtSetVal("length");
        jjtThis.cacheBegin = jmm.tokenCache[0];
        jjtThis.cacheEnd = jmm.tokenCache[1];}
}

// *** Terminals with Values
void Integer() : {Token token;}
{
    token = <INTEGERLITERAL>
    {jjtThis.jjtSetVal(token.image);}
}

void MethodName() : {Token token;}
{
    (token = <IDENTIFIER> | token = <MAIN>)
    {jjtThis.jjtSetVal(token.image);}
}

void ClassMethodName() #MethodName : {Token token;}
{
    (token = <IDENTIFIER> | token = <MAIN> | token = <LENGTH>)
    {jjtThis.jjtSetVal(token.image);}
}

void ClassType() : {Token token;}
{
    (token = <IDENTIFIER> | token = <MAIN> | token = <LENGTH>)
    {jjtThis.jjtSetVal(token.image);}
}

void Identifier() : {Token token;}
{
    (token = <IDENTIFIER> | token = <MAIN> | token = <LENGTH>)
    {jjtThis.jjtSetVal(token.image);}
}

// *** Expression
void Expression() #void : {}
{
    Term1() (Op1())*
}

void ExpressionTail() #void : {}
{
    (Op4())* (Op3())* (Op2())* (Op1())*
}

void LiteralExpression() #void : {}
{
    LiteralTerm4() ExpressionTail()
}

void IdentifierExpression() #void : {}
{
    IdentifierTerm4() ExpressionTail()
}

// *** Term
void Term1() #void : {}
{
    Term2() (Op2())*
}

void Term2() #void : {}
{
    Term3() (Op3())*
}

void Term3() #void : {}
{
    Term4() (Op4())*
}

void Term4() #void : {}
{
  LiteralTerm4()
  |
  IdentifierTerm4()
}

void LiteralTerm4() #void : {}
{
    Integer() TermTail()
    |
    (<TRUE> #True) TermTail()
    |
    (<FALSE> #False) TermTail()
    |
    (<THIS> #This) TermTail()
    |
    (<LCB> Expression() <RCB>) TermTail()
    |
    <NEW> NewTerm() TermTail()
    |
    NOT()
}

void IdentifierTerm4() #void : {}
{
    Identifier() TermTail()
}

void TermTail() #void : {}
{
    (ArrayAccess() | ObjectAccess())*
}

void ArrayAccess() #void : {}
{
    (<LSB> Expression() <RSB>) #Bracket(2)
}

void ObjectAccess() #void : {}
{
    <POINT> ObjectAccessTail()
}

void ObjectAccessTail() #void : {}
{
    Length() LengthTail()
    |
    MethodName() <LCB> ArgumentList() <RCB> #Call(3)
}

void LengthTail() #void : {}
{
    MethodNameLength() <LCB> ArgumentList() <RCB> #Call(3)
    |
    {} #Length(1)
}

void ArgumentList() : {}
{
    (Expression() (<COMMA> Expression())*)?
}

void NewTerm() #void : {}
{
    <INT> <LSB> Expression() <RSB> #NewIntArray(1)
    |
    ClassType() <LCB> <RCB> #NewClass(1)
}

// *** Operators
void Op1() #void : {}
{
    <AND> Term1() #AND(2)
}

void Op2() #void : {}
{
    <LT> Term2() #LT(2)
}

void Op3() #void : {}
{
    <SUM> Term3() #SUM(2)
    |
    <SUB> Term3() #SUB(2)
}

void Op4() #void : {}
{
    <MUL> Term4() #MUL(2)
    |
    <DIV> Term4() #DIV(2)
}

void NOT() : {}
{
    <NOT> Term4()
}

// *** Statements
// * Statement Types:
//   * BlockStatement(*)  { ... }
//   * IfElseStatement(3) if (Condition) TrueBranch else ElseBranch
//   * WhileStatement(2)  while (Condition) LoopBody
//   * PlainStatement(1)  Expression
//   * Assignment(2)      Target = Value
void Statement() #void : {}
{
    BlockStatement()
    |
    IfElseStatement()
    |
    WhileStatement()
    |
    LineStatement()
}

void BlockStatement() : {}
{
    <LEFTKEY> (Statement())* <RIGHTKEY>
}

void IfElseStatement() : {}
{
    <IF> <LCB> Expression() <RCB> //#Condition(1)
    Statement() //#TrueBranch(1)
    <ELSE> Statement() //#FalseBranch(1)
}

void WhileStatement() : {}
{
    try { <WHILE> <LCB> }
    catch (ParseException e) { e.advance(); }

    try { Expression()
        try { <RCB> }
        catch (ParseException e) { e.advance(); } }
    catch (ParseException e) { e.skipto(new int[]{RCB, IF, WHILE, LEFTKEY}).consumeif(RCB); }

    try { Statement() }
    catch (ParseException e) { e.skipto(new int[]{RIGHTKEY, SEMICOLON}).consume(); }
}

void LineStatement() #void : {}
{
    LiteralLineStatement()
    |
    IdentifierStatement()
}

// Expression ;   FOR NON IDENTIFIERS
void LiteralLineStatement() #void : {}
{
    LiteralExpression() <SEMICOLON> #PlainStatement(1)
}

// Identifier = Expression ;
// Identifier [ Expression ] = Expression ;
// Expression ;   FOR IDENTIFIERS
void IdentifierStatement() #void : {}
{
    Identifier() IdentifierStatementTail()
}

void IdentifierStatementTail() #void : {}
{
    (<EQUAL> Expression() #Assignment(2)
    |
    ArrayAccess() ArrayAccessIdentifierStatementTail()
    |
    ObjectAccess() TermTail() ExpressionTail()
    |
    ExpressionTail())
    <SEMICOLON> #PlainStatement(1)
}

void ArrayAccessIdentifierStatementTail() #void : {}
{
    <EQUAL> Expression() #Assignment(2)
    |
    TermTail() ExpressionTail()
}

void LiteralStatement() #void : {}
{
    BlockStatement()
    |
    IfElseStatement()
    |
    WhileStatement()
    |
    LiteralLineStatement()
}

// *** Types
void Type() #void : {}
{
    PrimitiveType()
    |
    ClassType()
}

void PrimitiveType() #void : {}
{
    IntType()
    |
    <BOOLEAN> #Boolean
    |
    <STRING> #String
}

void IntType() #void : {}
{
   (<INT> #Int) (<LSB> <RSB> #IntArray(1))?
}

// *** Method Body (Declarations and Statements)
void MethodBody() #void : {}
{
    PrimitiveType() Identifier() #VariableDeclaration(2)
    <SEMICOLON> MethodBody()
    |
    Identifier() AssignmentOrDeclaration()
    |
    (LiteralStatement() (Statement())*)?
}

void AssignmentOrDeclaration() #void : {}
{
    Identifier() <SEMICOLON> #VariableDeclaration(2) MethodBody()
    |
    IdentifierStatementTail() (Statement())*
}

// *** Parameters
void Parameter(): {}
{
    Type() Identifier()
}

void ParameterList(): {}
{
    (Parameter() (<COMMA> Parameter())*)?
}

void MainParameterList(): {}
{
    <STRING> <LSB> <RSB> Identifier()
}

// *** Methods
void MainDeclaration() : {}
{
    <STATIC>
    <VOID>
    <MAIN>
    <LCB>
    MainParameterList()
    <RCB>
    <LEFTKEY>
    MethodBody() #MethodBody
    <RIGHTKEY>
}

void MethodDeclaration() : {}
{
    Type() //#MethodReturnType
    ClassMethodName()
    <LCB>
    ParameterList()
    <RCB>
    <LEFTKEY>
    MethodBody() #MethodBody
    ReturnStatement()
    <RIGHTKEY>
}

void ReturnStatement() : {}
{
    <RETURN> Expression() <SEMICOLON>
}

// *** Class
void ClassDeclaration() : {}
{
    ClassHeader()
    ClassBody()
}

void ClassHeader() : {}
{
    <CLASS> ClassType() (Extends())?
}

void Extends() : {}
{
    <EXTENDS> ClassType()
}

void ClassBody() : {}
{
    <LEFTKEY>
    ClassVarDeclarations()
    ClassMethodDeclarations()
    <RIGHTKEY>
}

void ClassVarDeclaration() : {}
{
    Type() Identifier() <SEMICOLON>
}

void ClassMethodDeclaration() #void : {}
{
    <PUBLIC> (MainDeclaration() | MethodDeclaration())
}

void ClassVarDeclarations() : {}
{
    (ClassVarDeclaration())*
}

void ClassMethodDeclarations() : {}
{
    (ClassMethodDeclaration())*
}

// DONE: What to do with variables named "main", "length".
// DONE: What to do with types named "main", "length", "String".
// DONE: Add begin and end lines/columns (ranges) to output AST

/**
 * DONE: Range decisions:
 *  1. Should ClassHeader() include 'class'? [YES]
 *  2. Should Extends() include 'extends'? [YES]
 *  3. Should ClassBody() include '{' and '}'? [YES]
 *  4. Should ClassMethodDeclaration() include 'public'? [YES]
 *  4.1. Distinction between method and main? [NO]
 *  5. Should ArgumentList()/[Main]ParameterList() include '(' and ')'? [NO]
 *  6. Should NOT() include '!'? [YES]
 *  7. Should ReturnStatement() include 'return'? [YES]
 *  8. Should ReturnStatement() include ';'? [YES]
 *  ...
 */

// Keyword: one of
// abstract   continue   for          new         switch
// assert     default    if           package     synchronized
// boolean    do         goto         private     this
// break      double     implements   protected   throw
// byte       else       import       public      throws
// case       enum       instanceof   return      transient
// catch      extends    int          short       try
// char       final      interface    static      void
// class      finally    long         strictfp    volatile
// const      float      native       super       while
